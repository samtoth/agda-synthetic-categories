\date{2025-06-17}
\title{The Free Algebraically Injective HIT}
\taxon{module}
\meta{module}{\startverb axioms.FreeAlgInj \stopverb}
\author{samueltoth}
\import{stt-macros}

%```agda
\agda{
module axioms.FreeAlgInj where

open import ufAxioms
open import foundations.Prelude
open import core.Arrow
open import core.Lifts
open import core.PullbackPower
}
%```


\subtree[stt-005L]{
\taxon{definition}
\title{Weak orthogonators of maps}

\quiver{
\begin{tikzcd}
	A && X \\
	\\
	B && Y
	\arrow[from=1-1, to=1-3]
	\arrow["f"', from=1-1, to=3-1]
	\arrow["p", from=1-3, to=3-3]
	\arrow[dashed, from=3-1, to=1-3]
	\arrow[from=3-1, to=3-3]
\end{tikzcd}
}

\p{[Orthogonal maps](stt-004A) require uniqueness of
lifting solution to lifting problems as above. When this
uniqueness requirement is lifted we can speak of the type
of weak-orthogonators for a pair of maps, which give a chosen
lift for each lifting problem. When this type is merely
inhabited we say that a pair of maps are weakly orthogonal.}

%```agda
\agda{
weak-orthogonator : ∀ {𝓤 𝓥 𝓦 𝓛} {A : Type 𝓤} {B : Type 𝓥}
                          {C : Type 𝓦} {D : Type 𝓛}
                        → (f : A → B)
                        → (g : C → D)
                        → Type (𝓤 ⊔ 𝓥 ⊔ 𝓦 ⊔ 𝓛)
weak-orthogonator f g = ∀ (H : Arrow-map f g) → Lift H
}
%```
}


\subtree[stt-005N]{
\taxon{theorem}
\title{Weakly orthogonal maps give a section of the pullback power}

\p{A map #{f} is weakly orthogonal to #{g} iff the [pullback power](stt-004D)
 #{f \pitchfork g} has a section.}

%```agda
\agda{
sec-pb-power←weak-orthogonator
  : ∀ {𝓤 𝓥 𝓦 𝓛} {A : Type 𝓤} {B : Type 𝓥}
      {C : Type 𝓦} {D : Type 𝓛}
      {f : A → B} {g : C → D}
    → weak-orthogonator f g
    → section (pullback-power f g)
sec-pb-power←weak-orthogonator {f = f} {g} lift
  = section←fibres _ fib where
  fib : ∀ (H : Arrow-map f g) → fibre (pullback-power f g) H
  fib H =  _≃_.bwd (pb-power-fibre≃lifts _ _ _) (lift H)

sec-pb-power≃weak-orthogonator
  : ∀ {𝓤 𝓥 𝓦 𝓛} {A : Type 𝓤} {B : Type 𝓥}
      {C : Type 𝓦} {D : Type 𝓛}
      {f : A → B} {g : C → D}
    → weak-orthogonator f g
    ≃ section (pullback-power f g)
sec-pb-power≃weak-orthogonator {f = f}{g}
  = Π (Arrow-map f g) Lift
      ≃⟨ precomp-Π-≃ (λ c → pb-power-fibre≃lifts _ _ c e⁻¹) ⟩
    Π (Arrow-map f g) (fibre (pullback-power f g))
      ≃⟨ section≃fibres _ ⟩
    section (pullback-power f g) ≃∎
}
%```
}

\subtree[stt-005O]{
\title{Weak orthogonators for types}
\taxon{definition}

\p{A weak orthogonator for a type mirrors the definitions
for the stronger notion of [orthogonality at types](stt-004O).
We require not that the precomposition by #{f} is an equivalence,
but instead talk about the \em{type} of sections to postcomposition by #{f}.
}

\p{A weak orthogonator for type #{X} with respect to a map #{f : A \to B}
is a section of #{X^f : (B \to X) \to (A \to X)}.}

%```agda
\agda{
weak-type-orthogonator
  : ∀ {𝓤 𝓥 𝓦} {A : Type 𝓤} {B : Type 𝓥}
      (f : A → B) (X : Type 𝓦)
    → Type (𝓤 ⊔ 𝓥 ⊔ 𝓦)
weak-type-orthogonator f X = section (postcomp X f)
}
%```
}

\subtree[stt-005P]{
\taxon{theorem}

\p{Mirroring the [corresponding theorem](stt-004Q) for orthogonality,
we can say that the type of weak type orthogonators for #{X} against #{f}
is equivalent to the type of weak orthoginators for the map #{! : A \to 1}
against #{f}.}

\todo{Proof!}

%```agda
\agda{
-- weak-type-orthogonator-!
--   : ∀ {𝓤 𝓥 𝓦} {A : Type 𝓤} {B : Type 𝓥}
--       {f : A → B} {X : Type 𝓦}
--     → weak-type-orthogonator f X
--     ≃ weak-orthogonator f (! {A = X})
-- weak-type-orthogonator-! = {!!}
}
%```
}


\subtree[stt-005J]{
\taxon{definition}
\title{Algebraically injective types}

\p{Given a family of maps #{f_i : A_i \to B_i}, we say that a type
#{X} is \em{algebraically} #{f\rm{-injective}} if for any family of maps
#{a_i : A_i \to X}, there is a chosen extension of #{a} along #{f_i}, that
is, the following diagram commutes for all #{i}: }

\quiver{
\begin{tikzcd}
	{A_i} && X \\
	\\
	{B_i}
	\arrow["{a_i}", from=1-1, to=1-3]
	\arrow["{f_i}"', from=1-1, to=3-1]
	\arrow[dashed, from=3-1, to=1-3]
\end{tikzcd}
}

\p{We observe that this definition is equivalent to asking for
a chosen [weak orthogonator](stt-005O) for #{X} against each #{f_i}.}

%```agda
\agda{
Algebraic-injective
  : ∀ {𝓘 𝓤 𝓥 𝓦} {I : Type 𝓘}
      {A : I → Type 𝓤} {B : I → Type 𝓥}
      (f : (i : I) → A i → B i)
      (X : Type 𝓦)
     → Type (𝓘 ⊔ 𝓤 ⊔ 𝓥 ⊔ 𝓦)
Algebraic-injective f X = ∀ i → weak-type-orthogonator (f i) X

lift-alg-inj : ∀ {𝓘 𝓤 𝓥 𝓦} {I : Type 𝓘}
      {A : I → Type 𝓤} {B : I → Type 𝓥}
      {f : (i : I) → A i → B i}
      {X : Type 𝓦}
     → Algebraic-injective f X
     → (i : I) (p : A i → X)
     → B i → X
lift-alg-inj alg i = alg i .fst
}
%```
}

\subtree[stt-005I]{
\taxon{definition}
\title{Free algebraically-injective types}
\citek{rijkeMod2020}

\p{Given a family of types #{f_i : A_i \to B_i} and a type #{X}, we can
define the free algebraically #{f\rm{-injective}} type on #{X}, called
#{\mathcal{J}_{f}(X)} with the following
higher inductive type:}

\ul{
\li{#{\eta : X \to \mathcal{J}_{f}(X)}}
\li{#{\rm{ext}_i : (A_i \to \mathcal{J}_{f}(X)) \to (B_i \to \mathcal{J}_{f}(X))}}
\li{#{\rm{isext}_i : \Pi_{(g : A_i \to \mathcal{J}_{f}(X))} \Pi_{(a : A_i)}
                     \rm{ext}_i(g,f(a)) = g(a)}}
}

\p{In plain agda we have to define higher inductive types via
postulates.}

%```agda
\agda{
module _ {𝓘 𝓤 𝓥 𝓦} {I : Type 𝓘} {A : I → Type 𝓤}
         {B : I → Type 𝓥} where
  postulate
    Free-inj : (f : ∀ i → A i → B i) → Type 𝓦 → Type 𝓦

  module Free-inj {f : ∀ i → A i → B i} {X : Type 𝓦} where
    postulate
      inc   : X → Free-inj f X
      ext : ∀ {i} → (g : A i → Free-inj f X)
                       → B i → Free-inj f X
      is-ext : ∀ {i} → (g : A i → Free-inj f X)
                          (a : A i) → ext g (f i a) ＝ g a

      ind : ∀ {𝓜} (P : Free-inj f X → Type 𝓜)
              (N : (x : X) → P (inc x))
              (R : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                   → ∀ (b : B i) → P (ext g b))
              (S : ∀ {i} (g : A i → Free-inj f X)
                    (h : (a : A i) → P (g a))
                    (a : A i)
                   → tr P (is-ext g a) (R g h (f i a)) ＝ h a)
            → (x : Free-inj f X) → P x

      ind-inc : ∀ {𝓜} {P : Free-inj f X → Type 𝓜}
                  {N : (x : X) → P (inc x)}
                  {R : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                       → ∀ (b : B i) → P (ext g b)}
                  {S : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                         (a : A i)
                       → tr P (is-ext g a) (R g h (f i a)) ＝ h a}
                 → ∀ x → ind P N R S (inc x) ＝ N x

      ind-ext : ∀ {𝓜} {P : Free-inj f X → Type 𝓜}
                  {N : (x : X) → P (inc x)}
                  {R : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                       → ∀ (b : B i) → P (ext g b)}
                  {S : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                         (a : A i)
                       → tr P (is-ext g a) (R g h (f i a)) ＝ h a}
                 → ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                   → (b : B i) → ind P N R S (ext g b) ＝ R g h b

    ind-ext-base : ∀ {𝓜} {P : Free-inj f X → Type 𝓜}
                  {N : (x : X) → P (inc x)}
                  {R : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                       → ∀ (b : B i) → P (ext g b)}
                  {S : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                         (a : A i)
                       → tr P (is-ext g a) (R g h (f i a)) ＝ h a}
                  → ∀ {i} (g : A i → Free-inj f X)
                  → (b : B i)
                  → ind P N R S (ext g b)
                      ＝
                     R g (ind P N R S ∘ g) b
    ind-ext-base g = ind-ext g _

    {-# REWRITE ind-inc ind-ext-base #-}
    postulate
      ind-is-ext : ∀ {𝓜} {P : Free-inj f X → Type 𝓜}
                  {N : (x : X) → P (inc x)}
                  {R : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                       → ∀ (b : B i) → P (ext g b)}
                  {S : ∀ {i} (g : A i → Free-inj f X)
                         (h : (a : A i) → P (g a))
                         (a : A i)
                       → tr P (is-ext g a) (R g h (f i a)) ＝ h a}
                 → ∀ {i} (g : A i → Free-inj f X)
                      (a : A i)
                 → apᵈ (ind P N R S) (is-ext g a)
                    ＝
                   S g (ind P N R S ∘ g) a
    {-# REWRITE ind-is-ext #-}
}
%```
}

\subtree[stt-0060]{
\title{Understanding the recursion principal of the Free algebraic injective}

\todo{TODO!}
}

\subtree[stt-005W]{
\date{2025-06-18}
\title{Free algebraically-injective types are algebraically-injective}
\taxon{theorem}

%```agda
\agda{
module _ {𝓘 𝓤 𝓥 𝓦} {I : Type 𝓘} {A : I → Type 𝓤}
         {B : I → Type 𝓥} where
  injector-Free-inj
    : ∀ {f : (i : I) → A i → B i}
        {X : Type 𝓦}
      → Algebraic-injective f (Free-inj f X)
  injector-Free-inj i .fst = Free-inj.ext
  injector-Free-inj i .snd α
    = funext→ (Free-inj.is-ext α)
}
%```
}

\subtree[stt-0068]{
\title{Morphisms of algebraic injectives}
\date{2025-06-21}

\p{Given a pair of algebraic injectives #{(X,x)} and #{(Y,y)} over
the same family of maps, #{f_i}, we define the type of morphisms
of algebraic injectives: #{\Hom((X,x), (Y,y))} to be the
type of maps which commute coherently with the given extensions.}

\quiver{
\begin{tikzcd}
	& X \\
	{A_i} && Y \\
	\\
	{B_i}
	\arrow["\alpha", from=1-2, to=2-3]
	\arrow["{p_x}", from=2-1, to=1-2]
	\arrow["{\alpha p_x}"{description}, from=2-1, to=2-3]
	\arrow["{f_i}"', from=2-1, to=4-1]
	\arrow[dashed, from=4-1, to=1-2]
	\arrow[dashed, from=4-1, to=2-3]
\end{tikzcd}
}

%```agda
\agda{
record Algebraic-injective-map
  {𝓘 𝓤 𝓥 𝓦 𝓜} {I : Type 𝓘}
      {A : I → Type 𝓤} {B : I → Type 𝓥}
      {f : (i : I) → A i → B i}
      {X : Type 𝓦} (X-alg : Algebraic-injective f X)
      {Y : Type 𝓜} (Y-alg : Algebraic-injective f Y)
      (α : X → Y)
    : Type (𝓘 ⊔ 𝓤 ⊔ 𝓥 ⊔ 𝓦 ⊔ 𝓜) where
  field
    ext-comm : ∀ i p → (α ∘ lift-alg-inj X-alg i p)
                         ~
                      lift-alg-inj Y-alg i (α ∘ p)
    ext-comm-coh : ∀ i → (precomp (A i) α ◂ X-alg i .snd)
                           ~
                          (λ a → funext→ (ext-comm i a ∘ f i))
                            ~∙
                            Y-alg i .snd ▸ precomp (A i) α
}
%```
}

\subtree[stt-0067]{
\date{2025-06-21}
\title{The universal property for free algebraic injectives}
\taxon{theorem}

\p{We show that any map into an algebraically injective type
factors through the free algebraically injective type. In other
words, given types #{X} and #{Y}, where #{X} is an algebraically
injective type, the map #{- \circ \eta : (\mathcal{J}_{f}(X) \to Y) \to
(X \to Y)} is an equivalence.}

\todo{Proof: Todo}

%```agda
\agda{
postcomp-alg-inj : ∀ {𝓘 𝓤 𝓥 𝓦 𝓜} {I : Type 𝓘}
                     {A : I → Type 𝓤} {B : I → Type 𝓥}
                     {f : (i : I) → A i → B i}
                     (X : Type 𝓦) {Y : Type 𝓜}
                     (Y-alg : Algebraic-injective f Y)
                   → Σ[ α ∶ (Free-inj f X → Y) ]
                       Algebraic-injective-map injector-Free-inj Y-alg α
                   → (X → Y)
postcomp-alg-inj X {Y} Y-alg = postcomp Y Free-inj.inc ∘ fst

-- alg-inj-map-factors : ∀ {𝓘 𝓤 𝓥 𝓦 𝓜} {I : Type 𝓘}
--                         {A : I → Type 𝓤} {B : I → Type 𝓥}
--                         {f : (i : I) → A i → B i}
--                         (X : Type 𝓦) {Y : Type 𝓜}
--                         (Y-alg : Algabraic-injective f Y)
--                       → is-equiv (postcomp-alg-inj {f = f} X Y-alg)
-- alg-inj-map-factors {f = f} X {Y} Y-alg = {!!}
}
%```
}
