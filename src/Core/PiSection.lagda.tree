\date{2025-06-26}
\author{samueltoth}
\title{Pi types as sections}
\taxon{module}
\meta{module}{\startverb Core.PiSection \stopverb}
\import{stt-macros}


%```agda
\agda{
module Core.PiSection where

open import Foundations.Prelude
open import ufAxioms
}
%```


\subtree[stt-006J]{
\taxon{theorem}
\title{Pi types are sections}

\p{We show that the type #{\Pi_{(a : A)} B(a)} is equivalent to
the type of sections of #{\pi : \Sigma_{(a : A)} B(a)}.}


%```agda
\agda{
section←Π : ∀ {𝓤 𝓥} {A : Type 𝓤}
              {B : A → Type 𝓥}
            → Π _ B
            → section (fst {B = B})
section←Π {B = B} f = ((λ x → (x , f x)) , ~refl)


section≃Π : ∀ {𝓤 𝓥} {A : Type 𝓤}
              {B : A → Type 𝓥}
            → section (fst {B = B})
            ≃ Π _ B
section≃Π {A = A} {B}
  = section fst                                  ≃⟨ Σ-Π-swap≃ _ _ e⁻¹ ⟩
    (∀ (a : A) → Σ[ x ∶ Σ A B ] (fst x ＝ a))    ≃⟨ Π-ap-≃ (λ a → Σ-assoc) ⟩
    ((a : A) → Σ[ a' ∶ A ] ((B a') × (a' ＝ a))) ≃⟨ Π-ap-≃ (λ a
                                                    → Σ-ap-≃ (λ a' → ×-swap)) ⟩
    (((a : A) → Σ[ a' ∶ A ] ((a' ＝ a) × B a'))) ≃⟨ Π-ap-≃ (λ a → Σ-＝singl) ⟩
    Π A B ≃∎

Π←section : ∀ {𝓤 𝓥} {A : Type 𝓤}
              {B : A → Type 𝓥}
            → section (fst {B = B})
            → Π _ B
Π←section {B = B} (f , s) a = tr B (s a) (snd (f a))
}
%```
}
