\date{2025-02-21}
\title{Homotopies of functions}
\taxon{definition}

\import{stt-macros}

% ```agda
\agda{
module foundations.Homotopy where

open import foundations.Functions
open import foundations.universe
open import foundations.Identity
open import foundations.PathReasoning
}
% ```

\p{A homotopy between functions is a pointwise equality of functions.}

% ```agda
\agda{
infix 10 _~_
_~_ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} → (f g : (a : A) → B a) → Type (𝓤 ⊔ 𝓥)
f ~ g = ∀ a → f a ＝ g a

}
% ```

\subtree{
\taxon{theorem}
\title{Homotopies are 2-cells in the #{(\infty, 1)}-category of types}

\proof{
% ```agda
\agda{

infixr 25 _~∙_ 
_~∙_ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} 
     → {f g : (a : A) → B a} → f ~ g → {h : (a : A) → B a} → g ~ h → f ~ h
(p ~∙ q) a = p a ∙ q a


~refl : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} {f : (a : A) → B a} → f ~ f
~refl a = refl

~∙-reflr : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} {f g : (a : A) → B a} → (p : f ~ g) → p ~∙ ~refl ~ p
~∙-reflr p a = ∙-reflr (p a)


infix 20 _~⁻¹
_~⁻¹ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} → {f g : (a : A) → B a} → f ~ g → g ~ f 
(p ~⁻¹) a = sym (p a)

~∙-sym : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} {f g : (a : A) → B a} → (p : f ~ g) → p ~∙ (p ~⁻¹) ~ ~refl
~∙-sym p a = ∙-sym (p a)


_~⟨⟩_ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} (f : (a : A) → B a) {g : (a : A) → B a} → f ~ g → f ~ g
x ~⟨⟩ x~y = x~y
infixr 2 _~⟨⟩_

~⟨⟩-syntax : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} → (f : (a : A) → B a) {g h : (a : A) → B a} → g ~ h → f ~ g → f ~ h
~⟨⟩-syntax f p q = q ~∙ p

infixr 2 ~⟨⟩-syntax
syntax ~⟨⟩-syntax x q p = x ~⟨ p ⟩ q

infix  3 _~∎
_~∎ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} → (f : (a : A) → B a) → f ~ f
x ~∎ = ~refl

infixr 25 _⟩~∙⟨_
_⟩~∙⟨_ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} {f g : A → B} {p q : f ~ g} {h : A → B} → {r : g ~ h} {s : g ~ h}
       → p ~ q → r ~ s → p ~∙ r ~ q ~∙ s
(p ⟩~∙⟨ q) a = (p a) ⟩∙⟨ (q a)


}
% ```
}
}

\p{We can define the whiskering of homotopies.}


% ```agda
\agda{

infixr 30 _◂_
_◂_ : ∀ {𝓤 𝓥 𝓦} {A : Type 𝓤} {B : Type 𝓥} {C : Type 𝓦}
    → (h : B → C) {f g : A → B} → f ~ g → h ∘ f ~ h ∘ g
(h ◂ p) x = ap h (p x)

infixl 31 _▸_
_▸_ : ∀ {𝓤 𝓥 𝓦} {A : Type 𝓤} {B : Type 𝓥} {C : Type 𝓦}
    → {f g : B → C} → f ~ g → (h : A → B) → f ∘ h ~ g ∘ h
(p ▸ h) x = p (h x)


infixr 28 _⟩~◂⟨_
_⟩~◂⟨_ : ∀ {𝓤 𝓥 𝓦} {A : Type 𝓤} {B : Type 𝓥} {C : Type 𝓦}
           (f : B → C) {g h : A → B} {p q : g ~ h} → p ~ q → f ◂ p ~ f ◂ q
(f ⟩~◂⟨ p) a = ap (ap f) (p a)

}
% ```

% ```agda
\agda{

--     ⌜ ap (f ∘ g ∘ f) (η x) ∙ ε (f x)
-- =     ε (f (g (f x)))      ∙ ap f (η x)

--   f ◂ g ◂ f ◂ ε
--   f ◂ ε ▸ g ▸ f

--    η ▸ f ▸ g ▸ f ~∙ f ◂ ε
--    f ◂ g ◂ f ◂ ε ~∙ η ▸ f

homotopy-natural : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} {f g : A → B} → (h : f ~ g)
                 → ∀ {x y} (p : x ＝ y) → h x ∙ ap g p ＝ ap f p ∙ h y  
homotopy-natural _ refl = ∙-reflr _

homotopy-inverse : ∀ {𝓤} {A : Type 𝓤} (f : A → A) (p : f ~ id) → p ▸ f ~ f ◂ p
homotopy-inverse {𝓤} {A} f p x = p (f x)                ＝⟨ ∙.insertl A (∙-sym (p x)) ⟩
                         (p (f x) ∙ p x) ∙ sym (p x)    ＝⟨ II x ⟩
                         (ap f (p x) ∙ p x) ∙ sym (p x) ＝⟨ ∙.cancell A (∙-sym (p x)) ⟩
                         ap f (p x) ∎  where
  I : ∀ x → p (f x) ∙ p x ＝ ap f (p x) ∙ p x
  I x = homotopy-natural p (p x)

  -- funext I : p ▸ f ~∙ p ＝ f ◂ p ~∙ p
  
  II : ∀ x → (p (f x) ∙ p x) ∙ sym (p x) ＝ (ap f (p x) ∙ p x) ∙ sym (p x)
  II x = ap (_∙ sym (p x)) (I x)
}
% ```
