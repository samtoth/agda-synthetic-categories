\taxon{module}
\title{(Reflective) Subuniverses}
\author{samueltoth}
\date{2025-05-09}
\meta{module}{\startverb modalities.Subuniverses \stopverb}
\import{stt-macros}

% ```agda
\agda{
module modalities.Subuniverses where

open import foundations.Prelude
open import ufAxioms
}
% ```


\subtree[stt-003Z]{
\taxon{definition}
\title{Subuniverse}

\p{A subuniverse is a subtype of some universe}

% ```agda
\agda{
SubU : ∀ (𝓤 𝓥 : Level) → Type (lsuc (𝓤 ⊔ 𝓥))
SubU 𝓤 𝓥 = Subtype (Type 𝓤) 𝓥
}
% ```
}

\subtree[stt-0040]{
\title{Reflector}
\taxon{defintion}
\meta{defines}{\startverb ["Reflector", "Reflector.○", "Reflector.○∈S",
                           "Reflector.η-○", "Reflector.reflects", "mk-rfltor"] \stopverb}
\p{Given a subuniverse #{S} of #{\UU}, we say that a type #{A : \UU} has a reflection
if there is a type #{○ A} in #{S} and a map #{\eta_○ : A \to \circ A} such that any map from #{A}
into a type in #{S} factors through #{\eta}.}

% ```agda
\agda{
record Reflector {𝓤 𝓥} (S : SubU 𝓤 𝓥) (A : Type 𝓤) : Type (lsuc 𝓤 ⊔ 𝓥) where
  constructor mk-rfltor
  field
    {○} : Type 𝓤
    ○∈S : ○ ∈ S
    ○-η : A → ○
    reflects : ∀ {C : Type 𝓤} → C ∈ S → is-equiv (postcomp C ○-η)

  ○-elim : ∀ {C : Type 𝓤} → C ∈ S → (A → C) → (○ → C)
  ○-elim cs = is-equiv.bwd (reflects cs)
}
% ```
}

\subtree[stt-0042]{
\title{A subuniverse contains #{\bigcirc} modal types}
\taxon{theorem}

\p{In [[modalities.HigherModality]], we start by defining the reflectors, and say that a type
is #{\bigcirc}-modal if the reflection map is an equivalence. In this setting, this definition
is a theorem.}

% ```agda
\agda{
in-subU←η-is-equiv : ∀ {𝓤 𝓥} {S : SubU 𝓤 𝓥}
                       {A : Type 𝓤}
                     → (RA : Reflector S A)
                     → is-equiv (RA .Reflector.○-η)
                     → A ∈ S
in-subU←η-is-equiv {S = S} RA eq
  = tr (_∈ S)
       (ua (mk≃ ○-η eq e⁻¹))
       ○∈S where open Reflector RA
}
% ```

\p{We can also show that for any type in #{S}, #{\eta_\circ} has a retract}

% ```agda
\agda{
η-has-retract←in-subU : ∀ {𝓤 𝓥} {S : SubU 𝓤 𝓥}
                       {A : Type 𝓤}
                       (RA : Reflector S A)
                       → A ∈ S
                       → retract (RA .Reflector.○-η)
η-has-retract←in-subU RA a∈S = (bwd id , happly (ε id)) where
  open Reflector RA
  open is-equiv (reflects a∈S)
}
% ```

}

\subtree[stt-0041]{
\title{Reflective subuniverse}
\taxon{definition}
\meta{defines}{\startverb ["is-reflective", "mk-reflective", "is-reflective.has-reflectors"] \stopverb}
\p{A subuniverse is reflective if it has all reflectors.}

% ```agda
\agda{
record is-reflective {𝓤 𝓥} (S : SubU 𝓤 𝓥) : Type (lsuc 𝓤 ⊔ 𝓥) where
  constructor mk-reflective
  field
    has-reflectors : ∀ A → Reflector S A

  module ○ {A} = Reflector (has-reflectors A)
  open ○ public hiding (○)

  ○ : Type 𝓤 → Type 𝓤
  ○ A = has-reflectors A .Reflector.○
}
% ```
}

\subtree[stt-004U]{
\date{2025-06-04}
\taxon{definition}
\title{Sigma closed reflective subuniverses}

\p{A reflective subuniverse \SS is said to be sigma closed when given
   #{A : \UU} and #{B : A \to \UU} such that #{A \in \SS} and #{B(a) \in \SS}
   for all #{a : A}, the type #{\Sigma_{(a : A)}B(a)} is in \SS.}

%```agda
\agda{
is-Σ-closed : ∀ {𝓤 𝓥} (S : SubU 𝓤 𝓥) → Type _
is-Σ-closed {𝓤} S = ∀ {A : Type 𝓤} {B : A → Type 𝓤}
                       → A ∈ S → (∀ a → B a ∈ S)
                       → (Σ[ a ∶ A ] B a) ∈ S
}
%```
}
