\date{2025-03-01}
\title{Univalence and function extensionality}
\meta{module}{\startverb Foundations.FunextUnivalence \stopverb}
\taxon{module}
\author{samueltoth}
\import{stt-macros}

% ```agda
\agda{
module Foundations.FunextUnivalence where

open import Foundations.Universes
open import Foundations.Identity
open import Foundations.Univalence
open import Foundations.CoherentIsomorphism
open import Foundations.QuasiIsomorphism
open import Foundations.Functions
open import Foundations.Sigma
open import Foundations.SigmaPath
open import Foundations.FibrewiseEquiv
open import Foundations.TotalEquiv
open import Foundations.Singleton
open import Foundations.EquivContrFibre
open import Foundations.EquivSingleton
open import Foundations.FunExt
open import Foundations.Homotopy
open import Foundations.EquivOfSingleton
open import Foundations.TheoremOfChoice
open import Foundations.SigmaProperties
}
% ```

\p{
In this module we show that univalence for a particular universe implies function extensionality in that universe.
}


\subtree[stt-000R]{
\taxon{lemma}
\meta{defines}{\startverb ["postcomp-≃←univalence"] \stopverb}
\p{Given a univalent universe #{\UU} and types #{A,B,X : \UU}, for any equivalence #{e : A \simeq B} there exists an equivalence: }

##{
(X \to A) \simeq (X \to B)
}

\proof{
\citet{4.9.1}{HoTT}

\p{This lemma follows directly from function extensionality as shown in [\code{postcomp-≃}](stt-000O), but we can avoid this and show
directly from univalence. Using the fact that assuming univalence, equivalences satsify the J-elimination rule, we can assume #{e} is the
identity equivalence, and then #{(X \to A) \simeq (X \to A)} is also just the identity equivalence.}

% ```agda
\agda{
postcomp-≃←univalence : ∀ {𝓤 𝓥} (u : is-univalent 𝓤) {A : Type 𝓤} {B : Type 𝓤} {X : Type 𝓥} → (e : A ≃ B) → (X → A) ≃ (X → B)
postcomp-≃←univalence uu {A} {B} {X} e = mk≃ (e ._≃_.fwd ∘_) (≃-ind (λ B' e' → is-equiv (e' ._≃_.fwd ∘_)) lem B e) where
  open WithLocalUnivalence uu
  lem : (B : Type _) (p : A ＝ B) → is-equiv (equiv←Id p ._≃_.fwd ∘_)
  lem _ refl = id-is-equiv
}
% ```
}
}

\subtree[stt-000S]{
\taxon{corollary}

\p{Given a family of singleton types #{P : A \to \UU}, the projection from the straightening is an equivalence. Then by
[stt-000R](the above lemma), we have: #{(A \to \Sigma_{(x : A)}P(x)) \simeq (A \to A)}.}

\proof{
\citet{4.9.3}{HoTT}
\p{The fibre of the projection #{\pi : \Sigma_{(x : A)}P(x) \to A} at some #{x : A} is equivalent to #{P(x)}. By assumption
we have the each #{P(x)} is singleton, and therefore so is each fibre of #{\pi}, thus it is an equivelence. }

% ```agda
\agda{
HoTT4-9-3 : ∀ {𝓤} (_ : is-univalent 𝓤) {A : Type 𝓤} {P : A → Type 𝓤} (P-sing : ∀ a → is-singleton (P a)) → (A → Σ A P) ≃ (A → A)
HoTT4-9-3 uu Ps = postcomp-≃←univalence uu (Σ-singleton Ps)
}
% ```
}
}

\subtree[stt-000T]{

\taxon{theorem}
\title{Univalence implies weak funext}


\meta{defines}{\startverb ["weak-funext"] \stopverb}

\proof{

\citet{4.9.4}{HoTT}

% ```agda
\agda{
module _ {𝓤} (uu : is-univalent 𝓤) {A : Type 𝓤} {P : A → Type 𝓤} (P-sing : ∀ a → is-singleton (P a)) where
  private
    α : (A → Σ A P) → A → A
    α = HoTT4-9-3 uu P-sing ._≃_.fwd

    α-is-eqv : is-equiv α
    α-is-eqv = HoTT4-9-3 uu P-sing ._≃_.has-is-eqv

  HoTT4-9-4 : ((a : A) → P a) ≃ fibre α id
  HoTT4-9-4 = equiv←qiso lem where
    lem : ((a : A) → P a) ≅ fibre α id
    lem ._≅_.fwd f = (λ a → (a , f a)) , refl
    lem ._≅_.fwd-iso .fst (f , p) a = tr P (happly p a) (snd (f a))
    lem ._≅_.fwd-iso .snd .fst f = refl
    lem ._≅_.fwd-iso .snd .snd a = is-prop←is-single
      (is-contr-map←is-equiv (α-is-eqv) id) _ a

  weak-funext : is-singleton ((a : A) → P a)
  weak-funext = is-single←equiv-to-single (HoTT4-9-4 e⁻¹) (is-contr-map←is-equiv α-is-eqv id)
}
% ```
}
}

\subtree[stt-000U]{
\taxon{theorem}
\title{Weak funext gives full funext}

\meta{defines}{\startverb ["Weak-FunExt", "Weak-FunExtω", "FunExt←Weak-FunExt"] \stopverb}

% ```agda
\agda{
Weak-FunExt : ∀ 𝓤 → Type (lsuc 𝓤)
Weak-FunExt 𝓤 = ∀ {A : Type 𝓤} {B : A → Type 𝓤} → (∀ a → is-singleton (B a)) → is-singleton ((a : A) → B a)

Weak-FunExtω : ∀ 𝓤 → Typeω
Weak-FunExtω 𝓤 = ∀ {𝓥} {A : Type 𝓤} {B : A → Type 𝓥} → (∀ a → is-singleton (B a)) → is-singleton ((a : A) → B a)

Funext←Weak-FunExt : ∀ {𝓥} → Weak-FunExt 𝓥 → FunExt 𝓥
Funext←Weak-FunExt wfe {A = A} {B} {f} {g} = is-fibrewise-equiv←is-total-equiv fun-equiv g where
  fun : (Σ ((a : A) → B a) λ g → f ＝ g) → (Σ ((a : A) → B a) λ g → f ~ g)
  fun = total-map (λ _ → happly)

  I : is-singleton (Σ ((a : A) → B a) (λ g → f ~ g))
  I = is-single←equiv-to-single (Σ-Π-swap≃ _ λ x bx → f x ＝ bx) (wfe λ _ → Sing-is-singleton)
  
  fun-equiv : is-equiv fun
  fun-equiv = is-equiv←single-to-prop Sing-is-singleton
    (is-prop←is-single I)
    fun
}
% ```
}


\subtree[stt-000V]{
\taxon{corollary}
\title{Univalence implies function extensionality}

\meta{defines}{\startverb ["FunExt←UU"] \stopverb}

\p{We have show that univalence gives weak function extensionality, and weak fun-ext gives full fun-ext, so as an easy
corollary we have the univalence for a particular universe gives function extensionality for that universe}

% ```agda
\agda{
FunExt←UU : ∀ {𝓤} → is-univalent 𝓤 → FunExt 𝓤
FunExt←UU uu = Funext←Weak-FunExt (weak-funext uu)
}
% ```
}

