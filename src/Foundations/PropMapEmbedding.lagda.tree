\date{2025-03-17}
\taxon{module}
\title{Propositional maps are embeddings}
\meta{module}{\startverb Foundations.PropMapEmbedding \stopverb}
\import{stt-macros}

% ```agda
\agda{
module Foundations.PropMapEmbedding where

open import Foundations.Universes
open import Foundations.Functions
open import Foundations.Embedding
open import Foundations.EquivContrFibre
open import Foundations.EquivOfSingleton
open import Foundations.Identity
open import Foundations.Sigma
open import Foundations.CoherentIsomorphism
open import Foundations.QuasiIsomorphism
open import Foundations.Singleton
open import Foundations.FunctionInverses
open import Foundations.FibrewiseEquiv
open import Foundations.TotalEquiv
open import Foundations.SigmaPath
open import Foundations.EquivSingleton
open import Foundations.IdentityEquiv
}
% ```


% ```agda
\agda{
is-embedding←is-prop-map : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} → {f : A → B}
                         → is-prop-map f → is-embedding f
is-embedding←is-prop-map {f = f} pm {x} {y}
  = is-fibrewise-equiv←is-total-equiv {f = λ y p → ap f {b = y} p}
                                      I y where
  I : is-equiv (λ (a , b) → (a , ap f b))
  I = is-equiv←single-to-prop
         Sing-is-singleton
         (is-prop←equiv-to-prop (Σ-ap-≃ λ _ → sym≃) (pm (f x)))
         (total-map _)

fibre-single-at-vals←is-embedding
  : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} {f : A → B}
    → is-embedding f → ∀ a → is-singleton (fibre f (f a))
fibre-single-at-vals←is-embedding {f = f} emb a
  = is-single←equiv-to-single (Σ-ap-≃ (λ a₁ → mk≃ _ emb)) Sing'-is-singleton

is-prop-map←is-embedding
  : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} {f : A → B}
  → is-embedding f → is-prop-map f
is-prop-map←is-embedding femb b = is-prop←is-single-if-inhabited
  λ where (a , refl) → fibre-single-at-vals←is-embedding femb a
}
% ```
