\date{2025-04-20}
\title{The 3 for 2 property for equivalances}
\author{samueltoth}
\meta{module}{\startverb Foundations.3for2 \stopverb}

\import{stt-macros}

% ```agda
\agda{
module Foundations.3for2 where

open import Foundations.Universes
open import Foundations.Identity
open import Foundations.Homotopy
open import Foundations.FunctionInverses
open import Foundations.QuasiIsomorphism
open import Foundations.CoherentIsomorphism
open import Foundations.Functions
open import Foundations.Sigma
open import Foundations.Embedding
open import Foundations.EquivContrFibre
open import Foundations.Singleton
open import Foundations.EquivHomotopy
}
% ```

\subtree[stt-003E]{
\title{3 for 2 property for equivalences}
\taxon{theorem}
\meta{defines}{\startverb ["3-for-2", "3-for-2'",
                           "3-for-2~", "3-for-2~'"] \stopverb}

\p{Suppose you have a diagram of maps:}

\quiver{
\begin{tikzcd}
	A && C \\
	& B
	\arrow["{f \circ g}", from=1-1, to=1-3]
	\arrow["g"', from=1-1, to=2-2]
	\arrow["f"', from=2-2, to=1-3]
\end{tikzcd}
}

\p{The any two of the following imply the third:}

\ol{
\li{#{f} is an equivalence}
\li{#{g} is an equivalence}
\li{#{fg} is an equivalence}
}

% ```agda
\agda{
module _ {𝓤} {A : Type 𝓤}
         {𝓥} {B : Type 𝓥}
         {𝓦} {C : Type 𝓦}
         {f : B → C} {g : A → B} where

  3-for-2 : is-equiv f → is-equiv (f ∘ g) → is-equiv g
  3-for-2 fe fge = is-equiv←qiso qiso where
    module fg = is-equiv fge
    module f = is-equiv fe

    qiso : quasi-iso g
    qiso .fst = fg.bwd ∘ f
    qiso .snd .fst = fg.η
    qiso .snd .snd = (is-embedding←is-equiv fe) ◂emb (fg.ε ▸ f)

  3-for-2' : is-equiv g → is-equiv (f ∘ g) → is-equiv f
  3-for-2' ge fge = homotopy-is-equiv
                    (f ◂ g.ε)
                    (is-equiv-∘ fge (is-equiv⁻¹ ge)) where
    module g = is-equiv ge

  3-for-2~ : ∀ {k : A → C} (h : k ~ f ∘ g)
             → is-equiv f → is-equiv k → is-equiv g
  3-for-2~ h fe ke = 3-for-2 fe (homotopy-is-equiv h ke)

  3-for-2~' : ∀ {k : A → C} (h : k ~ f ∘ g)
             → is-equiv g → is-equiv k → is-equiv f
  3-for-2~' h ge ke = 3-for-2' ge (homotopy-is-equiv h ke)

  is-equiv~∘ : ∀ {k : A → C} (h : k ~ f ∘ g)
               → is-equiv f → is-equiv g → is-equiv k
  is-equiv~∘ h fe ge = homotopy-is-equiv (h ~⁻¹) (is-equiv-∘ fe ge)



  sec-3-for-2' : is-equiv g → section (f ∘ g) → section f
  sec-3-for-2' ge s@(sfg , p)
    = homotopy-section
        (f ◂ g.ε)
        (section-∘ {f = f ∘ g} {g = g.bwd}
                   s g.section-bwd) where
    module g = is-equiv ge

  sec-3-for-2~' : ∀ {k : A → C} (h : k ~ f ∘ g)
                 → is-equiv g → section k → section f
  sec-3-for-2~' H g s = sec-3-for-2' g (homotopy-section H s)

  sec-3-for-2 : is-equiv f → section (f ∘ g) → section g
  sec-3-for-2 fe s@(sfg , p)
    = (sfg ∘ f , fe ◂eqv (p ▸ f)) where
    module f = is-equiv fe

  sec-3-for-2~ : ∀ {k : A → C} (h : k ~ f ∘ g)
                 → is-equiv f → section k → section g
  sec-3-for-2~ H g s = sec-3-for-2 g (homotopy-section H s)
}
% ```
}
