\date{2025-02-24}
\title{Characterising the path space of fibre types}
\taxon{module}
\author{samueltoth}
\meta{module}{\startverb Foundations.FibrePath \stopverb}
\import{stt-macros}

% ```agda
\agda{
module Foundations.FibrePath where

open import Foundations.Universes
open import Foundations.Sigma
open import Foundations.SigmaPath
open import Foundations.FibrewiseEquiv
open import Foundations.Functions
open import Foundations.QuasiIsomorphism
open import Foundations.CoherentIsomorphism
open import Foundations.Identity
open import Foundations.DependentIdentity
}
% ```

\p{Although [fibres](stt-000M) are defined as sigma types - [who's path types can be characterised](Foundations.SigmaPath) - we can give a
particular special charecterisation of the path space of fibre types that will come in handy especially in proving that certain maps
have contractible or propositional fibres.
}

\subtree[stt-000N]{
\taxon{theorem}

\meta{defines}{\startverb ["fibre-path≃", "fibre-path→"] \stopverb}

\p{Given two terms #{(x,p),(y,q)} of #{\textrm{fibre}_f(y)}, we characterise the path type as follows:}

##{
 ((x,p) = (y,q)) \simeq (\Sigma_{(\gamma : x = y)} f(\gamma) \cdot q = p)
}


\proof{
We reason in a chain of equivalences:

##{
\begin{align}
(x,p) = (y,q) &\simeq && \Sigma_{(\gamma : x = y)} \gamma^* p = q \\
      	      &\simeq && \Sigma_{(\gamma : x = y)} f(\gamma) \cdot q = p
\end{align}
}

\ol{
\li{is the characterisation of paths in sigma types}
\li{follows directly from path induction on #{\gamma} and the fact path inversion is an equivalence}
}

% ```agda
\agda{
fibre-path≃ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} {f : A → B} {b : B} → ∀ {x y : fibre f b}
           → (x ＝ y) ≃ (Σ (x .fst ＝ y .fst) λ γ → ap f γ ∙ y .snd ＝ x .snd)
fibre-path≃ {x = a@(x , p)} {b@(y , q)}
  = (a ＝ b)                              ≃⟨ Σ-path e⁻¹ ⟩
    Σ (x ＝ y) (λ γ → IdP (ap _ γ) p q)   ≃⟨ Σ-ap-≃ lem ⟩
    Σ (x ＝ y) (λ γ → ap _ γ ∙ q ＝ p)    ≃∎ where

  lem2 : ∀ {𝓤} {A : Type 𝓤} {x y : A} (p : x ＝ y) (q : x ＝ y) → (p ＝ q) ≃ (q ＝ p)
  lem2 p q = equiv←qiso (mk-iso sym (sym , (λ _ → sym-sym) , λ _ → sym-sym))

  lem : (γ : x ＝ y) → (IdP (ap _ γ) p q) ≃ (ap _ γ ∙ q ＝ p)
  lem refl = lem2 p q 

fibre-path→ :  ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} {f : A → B} {b : B} → ∀ {x y : fibre f b}
           → (Σ (x .fst ＝ y .fst) λ γ → ap f γ ∙ y .snd ＝ x .snd) → (x ＝ y)
fibre-path→ = _≃_.bwd fibre-path≃
}
% ```
}

}
