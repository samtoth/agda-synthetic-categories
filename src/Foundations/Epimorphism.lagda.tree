\date{2025-04-20}
\title{Epimorphisms}
\taxon{module}
\author{samueltoth}
\meta{module}{\startverb Foundations.Epimorphisms \stopverb}
\import{stt-macros}

% ```agda
\agda{
open import Foundations.FunExt

module Foundations.Epimorphism (FE : FunExt-global) where

open import Foundations.Universes
open import Foundations.Embedding
open import Foundations.Functions
open import Foundations.Homotopy
open import Foundations.Identity
open import Foundations.CoherentIsomorphism
open import Foundations.CompositionEquiv
}
% ```

\subtree[stt-003F]{
\taxon{definition}
\title{epimorphism}
\meta{defines}{\startverb ["is-epi"] \stopverb}

\p{A map #{f : A \to B} is said to be an epimorphism if for all types
#{C}, the map #{- \circ f : (B \to C) \to (A \to C)} given by postcomposition
is an embedding.}

% ```agda
\agda{
is-epi : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥}
         → (f : A → B) → Typeω
is-epi f = ∀ {𝓦} (C : Type 𝓦) → is-embedding {A = _ → C} (_∘ f)


_▸epi_ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥}
            {f : A → B} {𝓦} {C : Type 𝓦}
            {g h : B → C}
            →  g ∘ f ＝ h ∘ f → is-epi f → g ＝ h
_▸epi_ {f = f} {g = g} {h = h} p ep
  = unap (ep _) {x = g} {y = h} p

is-epi←is-equiv : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥}
                    {f : A → B}
                  → is-equiv f → is-epi f
is-epi←is-equiv fe C
  = is-embedding←is-equiv (postcomp-equiv FE fe)
}
% ```
}
