\date{2025-02-26}
\title{Function extensionality}
\taxon{module}
\meta{module}{\startverb foundations.FunExt \stopverb}

\import{stt-macros}


\subtree[stt-0023]{
\title{Idea}

\p{
Function extensionality says that any homotopy between functions can be made into an equality between functions.

Or equivalently, that #{\textrm{happly} : f = g \to f \sim g} is an equivalence.
}

}

% ```agda
\agda{
module foundations.FunExt where

open import foundations.Universes
open import foundations.Functions
open import foundations.CoherentIsomorphism
open import foundations.Identity
open import foundations.DependentIdentity
open import foundations.Homotopy
open import foundations.Sigma
open import foundations.DependentHomotopy
}
% ```


\subtree[stt-0024]{
\title{Function extensionality}
\taxon{definition}

\meta{defines}{\startverb ["FunExt", "FunExtω", "FunExt-global"] \stopverb}

\p{Because function extensionality is a property of universes, we build up our definition in stages:
We first define function extensionality relative to a single universe, so that the predicate lives in the successor universe.

In order to account for maps from different universe levels, we also define a predicate on a single universe, which says that paths in maps
with domain type in any universe are characterised by homotopies - this quantifies over universes so must live in #{\UU^\omega}.

Finaly we define global function extensionality,
which says that all (canonical) universes satisfy function extensionality in the above sense, with this final definition of course also
living in #{\UU^\omega}.}

% ```agda
\agda{

FunExt : (𝓤 : Level) → Type (lsuc 𝓤)
FunExt 𝓤 = ∀ {A : Type 𝓤} {B : A → Type 𝓤} {f g : (a : A) → B a}
           → is-equiv (happly {f = f} {g})

FunExtω : Level → Typeω
FunExtω 𝓥 = ∀ {𝓤} → ∀ {A : Type 𝓤} {B : A → Type 𝓥} {f g : (a : A) → B a}
                   → is-equiv (happly {f = f} {g})

FunExt-global : Typeω
FunExt-global = ∀ {𝓥} → FunExtω 𝓥

}
% ```
}

% ```agda
\agda{
module WithFunExt {𝓥} (fe : FunExt 𝓥) where
  funext≃ : ∀ {A : Type 𝓥} {B : A → Type 𝓥} {f g : (a : A) → B a} → (f ＝ g) ≃ (f ~ g)
  funext≃ {𝓤} = mk≃ happly fe

  funext→ : ∀ {A : Type 𝓥} {B : A → Type 𝓥} {f g : (a : A) → B a} → f ~ g → f ＝ g
  funext→ p = is-equiv.bwd fe p 

  funext-J : ∀ {𝓦} {A : Type 𝓥} {B : A → Type 𝓥} {f : (a : A) → B a}
               → (M : (g : (a : A) → B a) → f ~ g → Type 𝓦) → M f ~refl
               → ∀ (g : (a : A) → B a) (h : f ~ g) → M g h
  funext-J M mrfl g h = tr (M g) (is-equiv.ε fe h) (J (λ g p → M g (happly p)) mrfl (funext→ h))

  funext-refl : ∀ {A : Type 𝓥} {B : A → Type 𝓥} {f : Π A B} → funext→ (~refl) ＝ refl {a = f}
  funext-refl = is-equiv.η fe refl

module WithFunExtω {𝓥} (fe : FunExtω 𝓥) where
  funext≃ : ∀ {𝓤} {A : Type 𝓤} {B : A → Type 𝓥} {f g : (a : A) → B a} → (f ＝ g) ≃ (f ~ g)
  funext≃ {𝓤} = mk≃ happly fe

  funext→ : ∀ {𝓤} {A : Type 𝓤} {B : A → Type 𝓥} {f g : (a : A) → B a} → f ~ g → f ＝ g
  funext→ p = is-equiv.bwd fe p 

  funext-J : ∀ {𝓤 𝓦} {A : Type 𝓤} {B : A → Type 𝓥} {f : (a : A) → B a}
               → (M : (g : (a : A) → B a) → f ~ g → Type 𝓦) → M f ~refl
               → ∀ (g : (a : A) → B a) (h : f ~ g) → M g h
  funext-J M mrfl g h = tr (M g) (is-equiv.ε fe h) (J (λ g p → M g (happly p)) mrfl (funext→ h))

  funext-refl : ∀ {𝓤} {A : Type 𝓤} {B : A → Type 𝓥} {f : Π A B} → funext→ (~refl) ＝ refl {a = f}
  funext-refl = is-equiv.η fe refl


module WithFunExt-global (fe : FunExt-global) where
  module FE {𝓥} = WithFunExtω {𝓥} fe
  open FE public


  Π-path : ∀ {𝓤 𝓥} {A : Type 𝓤} {B C : A → Type 𝓥}
           → B ＝ C → ((a : A) → B a) ＝ ((a : A) → C a)
  Π-path p = ap (λ f → (a : _) → f a) p

  funextᵈ→ : ∀ {𝓤 𝓥} {A : Type 𝓤} {B C : A → Type 𝓥} 
            {P : B ~ C}
            {f : (a : A) → B a}
            {g : (a : A) → C a}
            → f ~[ P ] g → f ＝[ ap (λ f → (a : A) → f a) (funext→ P)  ] g
  funextᵈ→ {A = A} {P = P} {f} = funext-J (λ C P → ∀ {g : (a : A) → C a} → f ~[ P ] g → f ＝[  Π-path (funext→ P) ] g)
                                 (funext-J (λ g h → f ＝[ Π-path (funext→ ~refl) ] g) (IdP-refl (ap Π-path funext-refl)) _) _ P 
                                 

}
% ```
