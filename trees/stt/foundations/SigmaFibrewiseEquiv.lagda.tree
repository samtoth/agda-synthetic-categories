\date{2025-03-04}
\title{The }



% ```agda
\agda{
module foundations.SigmaFibrewiseEquiv where

open import foundations.universe
open import foundations.QuasiIsomorphism
open import foundations.CoherentIsomorphism
open import foundations.Sigma
open import foundations.SigmaPath
open import foundations.Identity
open import foundations.DependentIdentity
open import foundations.Functions
open import foundations.EquivContrFibre
open import foundations.Singleton
open import foundations.SingletonEquivSingleton



fibre-totalisation : ∀ {𝓤 𝓥 𝓦} {A : Type 𝓤} {B : A → Type 𝓥} {B' : A → Type 𝓦} {f : ∀ a → B a → B' a}
                       {a : A} {b : B' a}
                     → fibre (totalisation f) (a , b) ≃ fibre (f a) b
fibre-totalisation {A = A} {B} {B'} {f = f} {a} {b}
  = fibre (totalisation f) (a , b)                                   ≃⟨⟩
    Σ (Σ A B) (λ (a' , ba) → totalisation f (a' , ba) ＝ (a , b))    ≃⟨ Σ-assoc ⟩
    Σ A (λ a' → Σ (B a') λ ba → totalisation f (a' , ba) ＝ (a , b)) ≃⟨⟩
    Σ A (λ a' → Σ (B a') λ ba → (a' , f a' ba) ＝ (a , b))           ≃⟨ Σ-ap-≃ (λ a' → Σ-ap-≃ λ ba → Σ-path e⁻¹) ⟩
    Σ A (λ a' → Σ (B a') λ ba → Σ (a' ＝ a)
        λ p → IdP B' (f a' ba) p b)                                  ≃⟨ (Σ-ap-≃ λ a' → Σ-comm) ⟩
    Σ A (λ a' → Σ (a' ＝ a) λ p → Σ (B a') 
        λ ba → IdP B' (f a' ba) p b)                                 ≃⟨ Σ-singl ⟩ 
    Σ (B a) (λ ba → f a ba ＝ b)                                     ≃⟨⟩
    fibre (f a) b ≃∎



is-total-equiv→is-fibrewise-equiv : ∀ {𝓤 𝓥 𝓦} {A : Type 𝓤} {B : A → Type 𝓥} {B' : A → Type 𝓦}
                                    → {f : ∀ a → B a → B' a} → is-equiv (totalisation f)
                                    → is-fibrewise-equiv f
is-total-equiv→is-fibrewise-equiv {f = f} teq a
  = is-contr-map→is-equiv λ
      b → equiv-singleton→singleton fibre-totalisation (is-equiv→is-contr-map teq (a , b))
  

}
% ```
