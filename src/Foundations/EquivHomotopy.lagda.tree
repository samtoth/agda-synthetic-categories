\date{2025-03-31}
\title{Stability of equivalence under homotopy}
\taxon{module}
\meta{module}{\startverb Foundations.EquivHomotopy \stopverb}
\import{stt-macros}


% ```agda
\agda{
module Foundations.EquivHomotopy where

open import Foundations.Universes
open import Foundations.QuasiIsomorphism
open import Foundations.CoherentIsomorphism
open import Foundations.FunctionInverses
open import Foundations.Sigma
open import Foundations.Homotopy
}
% ```


\p{Since being a quasi-isomorphism is stated via two homotopies, it is stable under homotopy. Hence being an equivalence is also stable under homotopy.}

% ```agda
\agda{
homotopy-section : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥}
                     {f g : A → B}
                   → (f ~ g)
                   → section f
                   → section g
homotopy-section H (h , sec) = (h , (H ~⁻¹) ▸ h ~∙ sec)

homotopy-retract : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥}
                     {f g : A → B}
                   → (f ~ g)
                   → retract f
                   → retract g
homotopy-retract H (h , ret) = (h , h ◂ (H ~⁻¹) ~∙ ret)

homotopy-qiso : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥}
              → {f g : A → B}
              → (f ~ g)
              → quasi-iso f
              → quasi-iso g
homotopy-qiso H (f⁻¹ , R , S) .fst = f⁻¹
homotopy-qiso {g = g} H (f⁻¹ , R , S) .snd .fst =  f⁻¹ ◂ (H ~⁻¹) ~∙ R
homotopy-qiso {g = g} H (f⁻¹ , R , S) .snd .snd =  (H ~⁻¹) ▸ f⁻¹ ~∙ S

homotopy-is-equiv :  ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥}
                  → {f g : A → B}
                  → (f ~ g)
                  → is-equiv f
                  → is-equiv g
homotopy-is-equiv H e = is-equiv←qiso (homotopy-qiso H (qiso←is-equiv e))
}
% ```
