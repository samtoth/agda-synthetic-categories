\date{2025-02-21}
\title{Higher Modalities}

\import{stt-macros}

% ```agda
\agda{
module modalities.HigherModality where

open import foundations.Prelude
open import ufAxioms
open import ergonomics.Core
}
% ```

\transclude{stt-000G}


% ```agda
\agda{
record HigherModality : Typeω where
  field
    ○_ : ∀ {𝓤} → Type 𝓤 → Type 𝓤
    ○-η : ∀ {𝓤} {A : Type 𝓤} → A → ○ A
    ○-ind : ∀ {𝓤 𝓥} {A : Type 𝓤} (P : ○ A → Type 𝓥) → (f : (a : A) → ○ P (○-η a)) → (ma : ○ A) → ○ (P ma)
    ○-β : ∀ {𝓤 𝓥} {A : Type 𝓤} (P : ○ A → Type 𝓥) → (f : (a : A) → ○ P (○-η a)) → (a : A) → ○-ind P f (○-η a) ＝ f a

  is-○-modal : ∀ {𝓤} → Type 𝓤 → Type 𝓤
  is-○-modal A = is-equiv (○-η {A = A})

  field
    ＝-is-○-modal : ∀ {𝓤} {A : Type 𝓤} {x y : A} → is-○-modal (x ＝ y)

  opaque
    ○-rec : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} → (A → ○ B) → (○ A → ○ B) 
    ○-rec = ○-ind (λ _ → _)

    ○-recβ :  ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} (f : A → ○ B) (a : A) → ○-rec f (○-η a) ＝ f a
    ○-recβ = ○-β (λ _ → _)

  unmod : ∀ {𝓤} {A : Type 𝓤} (m : is-○-modal A) → ○ A → A
  unmod (mk-eqv (g , _) _) = g
}
% ```

\subtree{
\taxon{theorem}
\title{#{\bigcirc A} is modal}

% ```agda
\agda{
  ○-elim : ∀ {𝓤} {A : Type 𝓤} → ○ (○ A) → ○ A
  ○-elim = ○-rec λ x → x

  ○-is-○-modal : ∀ {𝓤} {A : Type 𝓤} → is-○-modal (○ A)
  ○-is-○-modal = is-equiv←qiso the-iso where
    the-iso : quasi-iso ○-η
    the-iso .fst = ○-ind _ id
    the-iso .snd .fst = ○-β _ id
    the-iso .snd .snd a = unmod ＝-is-○-modal (○-ind (λ a → (○-η (○-ind _ id a)) ＝ a)
                 (λ a → ○-η (ap ○-η (○-β _ _ a))) a)
}
% ```
}

\subtree{
\taxon{definition}
\title{Modal induction}

% ```agda
\agda{
  opaque 
    ○-ind' : ∀ {𝓤 𝓥} {A : Type 𝓤} {P : ○ A → Type 𝓥} (P-mod : ∀ {a} → is-○-modal (P a))
             → (f : (a : A) → P (○-η a)) → (ma : ○ A) → P ma
    ○-ind' {P = P} is-mod f = (unmod is-mod) ∘ ○-ind P (○-η ∘ f)

    ○-β' : ∀ {𝓤 𝓥} {A : Type 𝓤} {P : ○ A → Type 𝓥}
           → (P-mod : ∀ {a} → is-○-modal (P a))
           → (f : (a : A) → P (○-η a)) → (a : A) → ○-ind' (λ {a'} → P-mod {a'}) f (○-η a) ＝ f a
    ○-β' is-mod f a = ap (unmod is-mod) (○-β _ _ a) ∙ is-equiv.η is-mod (f a)

    ○-ind'-qiso : ∀ {𝓤 𝓥} {A : Type 𝓤} {P : ○ A → Type 𝓥} {P-mod : ∀ {a} → is-○-modal (P a)}
                   → quasi-iso {A = (a : A) → P (○-η a)} {B = (a : ○ A) → P a}
                               (○-ind' P-mod)
    ○-ind'-qiso .fst = _∘ ○-η
    ○-ind'-qiso {P-mod = is-mod} .snd .fst f = ext! (○-β' is-mod f) 
    ○-ind'-qiso {P = P} {P-mod = is-mod} .snd .snd f = ext! (○-ind' {P = λ a → _ ＝ f a}
                     (λ {a} → ＝-is-○-modal {y = f a}) (○-β' is-mod (f ∘ ○-η)))

    ○-ind'-is-equiv : ∀ {𝓤 𝓥} {A : Type 𝓤} {P : ○ A → Type 𝓥}
                       {P-mod : ∀ {a} → is-○-modal (P a)}
                   → is-equiv {A = (a : A) → P (○-η a)} {B = (a : ○ A) → P a}
                               (○-ind' P-mod)
    ○-ind'-is-equiv {P-mod = is-mod} = is-equiv←qiso (○-ind'-qiso {P-mod = is-mod})

}
% ```
}


\subtree{
\taxon{universal property}
% ```agda
\agda{
module _ {M : HigherModality} where

  open HigherModality M

  ○-ind-qiso : ∀ {𝓤 𝓥} {A : Type 𝓤} {P : ○ A → Type 𝓥}
                 → quasi-iso {A = (a : A) → ○ P (○-η a)} {B = (a : ○ A) → ○ (P a)}
                            (○-ind P)
  ○-ind-qiso .fst = _∘ ○-η
  ○-ind-qiso .snd .fst f = ext! (○-β _ f)
  ○-ind-qiso {A = A} {P = P} .snd .snd f = ext! (○-ind' ＝-is-○-modal (○-β _ (f ∘ ○-η)))

  ○-ind-is-equiv : ∀ {𝓤 𝓥} {A : Type 𝓤} {P : ○ A → Type 𝓥}
                 → is-equiv {A = (a : A) → ○ P (○-η a)} {B = (a : ○ A) → ○ P a}
                            (○-ind P)
  ○-ind-is-equiv = is-equiv←qiso ○-ind-qiso


  open Universal

  instance
    Universal-○ : ∀ {𝓤 𝓥 𝓦} {A : Type 𝓤} {P : ○ A → Type 𝓥}
                 → ⦃ _ : ∀ {a} → is-○-modal (P a) ⦄
                 → ⦃ _ : Universal ((a : A) → P (○-η a)) 𝓦 ⦄
                 → Universal ((a : ○ A) → P a) 𝓦
    Universal-○ ⦃ mod ⦄ ⦃ u ⦄ .Universal.methods = u .methods
    Universal-○ ⦃ mod ⦄ ⦃ u ⦄ .Universal.from = ○-ind' mod ∘ u .from
    Universal-○ ⦃ mod ⦄ ⦃ u ⦄ .Universal.from-is-equiv = is-equiv-∘ ○-ind'-is-equiv (u .from-is-equiv)
}
% ```
}

\subtree{

\title{Instance resolution for is-modal}

% ```agda
\agda{
  instance
    ○-Modal : ∀ {𝓤} {A : Type 𝓤} → is-○-modal (○ A)
    ○-Modal = ○-is-○-modal

    ＝-Modal : ∀ {𝓤} {A : Type 𝓤} {a b : A} → is-○-modal (a ＝ b)
    ＝-Modal = ＝-is-○-modal

    Π-Modal : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥} → ⦃ _ : ∀ {x} → is-○-modal (B x) ⦄ → is-○-modal (∀ x → B x)
    Π-Modal = is-equiv←qiso (λ where
            .fst → λ f a → rec! (λ f → f a) f
            .snd .fst → λ f → ext! (λ a → ○-β' _ _ _)
            .snd .snd → λ f → ind! {B = λ f → ○-η (λ x → ○-ind' _ (λ f → f x) f) ＝ f}
                                   (λ f → ap ○-η (funext→ (λ a → ○-β' _ _ _))) f)



  ○-elim! : ∀ {𝓤} {A : Type 𝓤} ⦃ _ : is-○-modal A ⦄
            → ○ A → A
  ○-elim! = rec! id

  private module test where
    test : ∀ {𝓤} {A : Type 𝓤} → ○ ○ A → ○ A
    test = ○-elim!

    test-func : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} → (A → B) → ○ A → ○ B
    test-func f = rec! (λ a → ○-η (f a))
}
% ```


}

\subtree{
\taxon{theorem}
\title{Higher modalities are #{\Sigma}-closed}

% ```agda
\agda{
  ○-Σ-closed : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥}
            → ⦃ _ : is-○-modal A ⦄
            → ⦃ _ : ∀ {x} → is-○-modal (B x) ⦄
            → is-○-modal (Σ A B)
  ○-Σ-closed {A = A} {B = B} = is-equiv←qiso I where
    f : ○ Σ A B → A
    f = rec! (λ a _ → a)

    f-β : ∀ {a : A} {b : B a} → f (○-η (a , b)) ＝ a
    f-β = ○-β' _ _ _

    g : (x : ○ Σ A B) → B (f x)
    g = ind! (λ a b → tr B (sym f-β) b)

    g-β : ∀ {a : A} {b : B a} → IdP B b (sym f-β) (g (○-η (a , b)))
    g-β = sym (○-β' _ _ _)

    I : quasi-iso ○-η
    I .fst x = f x , g x
    I .snd .fst (a , b) = Σ-path→ (f-β , symP' B {p = f-β} g-β)
    I .snd .snd = ind! λ a b → ap ○-η (Σ-path→ (f-β , symP' B {p = f-β} g-β))


  instance
    Σ-Modal : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : A → Type 𝓥}
            → ⦃ _ : is-○-modal A ⦄
            → ⦃ _ : ∀ {x} → is-○-modal (B x) ⦄
            → is-○-modal (Σ A B)
    Σ-Modal = ○-Σ-closed 
}
% ```

}


\subtree{

% ```agda
\agda{

  modal←equiv-to-modal : ∀ {𝓤 𝓥} {A : Type 𝓤}
               → {B : Type 𝓥}
               → A ≃ B
               → is-○-modal A
               → is-○-modal B
  modal←equiv-to-modal eq ma = is-equiv←qiso I where
    open _≃_ eq

    instance
      A-modal : is-○-modal _
      A-modal = ma

    I : quasi-iso ○-η
    I .fst = fwd ∘ rec! bwd
    I .snd .fst b = ap fwd (○-β' _ _ _) ∙ ε b
    I .snd .snd = ind! (λ a → ap ○-η (ap fwd (○-β' _ _ _) ∙ ε a))

}
% ```

}


\subtree{
\taxon{theorem}
\title{Higher modalities are closed under finite products}

% ```agda
\agda{
  instance
    Modal-𝟙 : is-○-modal 𝟙
    Modal-𝟙 = is-equiv←single-dom←prop-cod 𝟙-is-singleton (ind! (λ _ _ → refl)) ○-η

  Modal-× : ∀ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥}
            → ⦃ _ : is-○-modal A ⦄ → ⦃ _ : is-○-modal B ⦄
            → is-○-modal (A × B)
  Modal-× = auto!
}
% ```

}
