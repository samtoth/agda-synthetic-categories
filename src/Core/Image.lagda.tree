\date{2025-08-01}
\title{Images of maps}
\taxon{module}
\meta{module}{Core.Image}
\author{samueltoth}
\import{stt-macros}

%```agda
\agda{
module Core.Image where

open import Foundations.Prelude
open import ufAxioms
open import Core.Slice
}
%```

\subtree[stt-008H]{
\title{Universal property of Images}
\taxon{definition}

\p{Given a map #{f : A \to B}, we say that the type of an embedding
#{i : I \to B}, together with a map #{\rm{Hom}_B(f,i)} in the slice is
an image of #{f} if any other embedding with a triangle as above
factors through #{i}.}

\p{In more precise language, for some embedding #{i' : I' \to B}
we can define a function #{(i \to i') \to f \to i'}. #{i} is the
image of #{f} when this map is an equivalence.}

%```agda
\agda{
module _ {𝓤 𝓥} {A : Type 𝓤} {B : Type 𝓥} (f : A → B) where
  postcomp-slice : ∀ {𝓦 𝓜} {I : Type 𝓦} {i : I → B}
                (K : Slice-map f i)
                {I' : Type 𝓜} {i' : I' → B}
              → Slice-map i i' → Slice-map f i'
  postcomp-slice {i = i} K {I'} {i'} sm
    = ( sm .fst ∘ K .fst
      , sm .snd ▸ K .fst ~∙ K .snd )

  is-universal-embedding : ∀ {𝓘} {I : Type 𝓘}
                             (i : I → B) → Slice-map f i
                           → Typeω
  is-universal-embedding i factors
    = ∀ {𝓦} {I' : Type 𝓦} (i' : I' → B)
      → is-embedding i'
      → is-equiv (postcomp-slice {i = i} factors {I'} {i'})

  is-universal-embedding←logical
    : ∀ {𝓘} {I : Type 𝓘} (i : I → B) (S : Slice-map f i)
      → (∀ {𝓦} {I' : Type 𝓦} (i' : I' → B)
          → is-embedding i'
          → Slice-map f i' → Slice-map i i' )
      → is-universal-embedding i S
  is-universal-embedding←logical i sl log i' i'emb
    = _≃_.has-is-eqv (is-equiv←logical
                       (slice-embedding-is-prop i'emb)
                       (slice-embedding-is-prop i'emb)
                       (_ , log i' i'emb))

  record is-image {𝓘} {I : Type 𝓘} (i : I → B) : Typeω where
    constructor mk-is-image
    field
      has-is-emb   : is-embedding i
      factors      : Slice-map f i
      has-is-image : is-universal-embedding i factors
}
%```
}
